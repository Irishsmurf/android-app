apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.apollographql.android'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    flavorDimensions 'variant'

    defaultConfig {
        applicationId 'me.echeung.moemoekyun'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 87
        versionName '5.1.0'

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            shrinkResources true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            minifyEnabled false
        }
    }

    productFlavors {
        playstore {
            dimension 'variant'
        }
        fdroid {
            dimension 'variant'

            applicationIdSuffix '.fdroid'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'

        enable 'ObsoleteSdkInt'

        abortOnError true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    kapt 'com.android.databinding:compiler:3.1.4'

    implementation 'org.koin:koin-android:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.fragment:fragment:1.2.1'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'com.squareup.moshi:moshi:1.8.0'

    implementation "com.apollographql.apollo:apollo-runtime:$apollo_version"
    implementation "com.apollographql.apollo:apollo-http-cache:$apollo_version"

    final glide_version = '4.11.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    final exoplayer_version = '2.11.2'
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-cast:$exoplayer_version"

    // Google Cast support (not available in fdroid flavour)
    playstoreImplementation 'com.google.android.gms:play-services-cast-framework:18.0.0'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

    // For detecting memory leaks; see https://square.github.io/leakcanary/
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0'
}

apollo {
    generateKotlinModels = true
}

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.apollo_version = '1.2.3'
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.apollographql.apollo:apollo-gradle-plugin:$apollo_version"
    }
}
